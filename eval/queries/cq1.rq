# CQ1: Culture conditions for HIGH productivity samples (new MCBO pattern)
# Process --RO:has_participant--> CellCultureSystem --RO:has_quality--> CultureConditionQuality

PREFIX mcbo: <http://example.org/mcbo#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro:   <http://purl.obolibrary.org/obo/RO_>

SELECT ?cellLine ?temperature ?pH ?dissolvedOxygen ?productivityType
WHERE {
  ?process a ?processType .
  ?processType rdfs:subClassOf* mcbo:CellCultureProcess .

  ?process mcbo:usesCellLine ?cellLine ;
           mcbo:hasProductivityMeasurement ?prodMeasure ;
           ro:0000057 ?system .                 # has_participant

  ?system a mcbo:CellCultureSystem .            # CRITICAL: prevents binding to cellLine
  ?system ro:0000086 ?ccq .                     # has_quality

  OPTIONAL { ?ccq mcbo:hasTemperature ?temperature }
  OPTIONAL { ?ccq mcbo:hasPH ?pH }
  OPTIONAL { ?ccq mcbo:hasDissolvedOxygen ?dissolvedOxygen }

  ?prodMeasure a ?productivityType .
  FILTER(?productivityType IN (
    mcbo:VeryHighProductivity,
    mcbo:HighProductivity,
    mcbo:MediumProductivity
  ))
}
ORDER BY ?productivityType ?cellLine

