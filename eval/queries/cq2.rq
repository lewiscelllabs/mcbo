PREFIX mcbo: <http://example.org/mcbo#>

SELECT ?choLine
       (GROUP_CONCAT(DISTINCT STR(?gene); separator=", ") AS ?genes)
       ?maxProductivity
       ?categoryAtMax
WHERE {
  {
    # Subquery to find the Max value per cell line
    SELECT ?choLine (MAX(?prodVal) AS ?maxProductivity)
    WHERE {
      ?choLine a mcbo:CHOCellLine .
      ?proc mcbo:usesCellLine ?choLine ;
            mcbo:hasProductivityMeasurement ?pm .
      ?pm mcbo:hasProductivityValue ?prodVal .
    }
    GROUP BY ?choLine
  }
  
  # Link back to get the genes and the specific category for that Max value
  ?choLine mcbo:overexpressesGene ?gene .
  ?proc mcbo:usesCellLine ?choLine ;
        mcbo:hasProductivityMeasurement ?pm .
  ?pm mcbo:hasProductivityValue ?maxProductivity .
  OPTIONAL { ?pm mcbo:hasProductivityCategory ?categoryAtMax . }
}
GROUP BY ?choLine ?maxProductivity ?categoryAtMax
ORDER BY DESC(?maxProductivity) ?choLine